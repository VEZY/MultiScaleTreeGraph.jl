# Add/remove nodes

```@setup usepkg
using MultiScaleTreeGraph
```

## Make an MTG manually

It is very easy to add or remove nodes in an MTG. Actually, we can even construct an MTG completely manually.

### Root node

Create the root node:

```@example usepkg
mtg = Node("node_1", MutableNodeMTG("/", "Plant", 0, 1), Dict{Symbol,Any}(:species => "Grassy-plant"))
```

The first argument to [`Node`](@ref) is the name. For the root node we usually use "node_1". The second argument is a [`MutableNodeMTG`](@ref) (or a [`NodeMTG`](@ref)) that defines the topology of the node: its link to its parent (the root node always decomposes), its symbol, its index and its scale. The third and last argument is used to add attributes to the MTG.

### Node name

The names of the nodes must be unique and indexed. This package provides a function that guaranties a new name is unique in the MTG:

```@example usepkg
new_name = MultiScaleTreeGraph.new_name(mtg)
```

### Adding children nodes

To create a child node, we use a different method of [`Node`](@ref). This method is close to the one we used for the root, except there is a new argument at the second position: the parent node. Here we use the root node as the parent (`mtg`), and the unique name (`new_name`) as the child name.


```@example usepkg
IN1 = Node(new_name, mtg, MutableNodeMTG("/", "Internode", 0, 2), Dict{Symbol,Any}(:diameter => 0.1, :length => 0.5))
```

Now the MTG has two nodes:

```@example usepkg
mtg
```

We can continue like this indefinitely. For example we can add a leaf to the first internode:

```@example usepkg
Node(MultiScaleTreeGraph.new_name(mtg), IN1, MutableNodeMTG("+", "Leaf", 0, 2), Dict{Symbol,Any}(:area => 0.2))
```

!!! note
    If a node has no children, there is no need to keep track of it in an object.

And an internode following the first internode:

```@example usepkg
IN2 = Node(MultiScaleTreeGraph.new_name(mtg), IN1, MutableNodeMTG("<", "Internode", 1, 2), Dict{Symbol,Any}(:diameter => 0.15, :length => 0.3))
```

And a leaf to it:

```@example usepkg
Node(MultiScaleTreeGraph.new_name(mtg), IN2, MutableNodeMTG("+", "Leaf", 1, 2), Dict{Symbol,Any}(:area => 0.2))
```

And here is our resulting MTG:

```@example usepkg
mtg
```

And the attributes:

```@example usepkg
DataFrame(mtg, get_attributes(mtg))
```

### Delete nodes

It is possible to remove a single node in an MTG using [`delete_node!`](@ref). For example if we want to delete the second internode ("node_4"):

```@example usepkg
mtg_del = deepcopy(mtg)

delete_node!(get_node(mtg_del, "node_4"))

mtg_del
```

As we can see the new MTG has only one internode now, and the children of the second internode are now the children of its parents, the first internode.

But what if we deleted the first internode?

```@example usepkg
mtg_del = deepcopy(mtg)

delete_node!(get_node(mtg_del, "node_2"))

mtg_del
```

We see there is a warning now. This is because we don't really know what should be the new link for a branching child replacing a decomposing node. So by default we don't make any assumption and keep the scale of the child as it is, in the hope the user will look into it.

In our example the first leaf is now branching from the plant, while it should decompose it because it is not of the same scale. But a leaf decomposing a Plant has no meaning botanically. The best approach would be to keep an intermediary node, as it was before.

The user can define its own rules for the new links using the `child_link_fun` keyword argument of [`delete_node!`](@ref). It expect a function that takes the child node as input and return its new link.

For example one could decide to never replace the children link and manage them afterward. In this case we can use the identity function like this:

```@example usepkg
mtg_del = deepcopy(mtg)

delete_node!(get_node(mtg_del, "node_2"), child_link_fun = node -> node.MTG.link)

mtg_del
```

It didn't change anything here because the child already kept its own link. But it will differ for other types of parent / children links.

## Insert/remove nodes programmatically

Sometimes we want to remove or add a lot of nodes in an MTG. This is possible to do it programmatically using [`delete_nodes!`](@ref) and [`insert_nodes`](@ref) respectively.

## Delete nodes

We can remove all nodes that meet specific conditions given by the usual node filters (see [Filters](@ref) for more details). For example one could remove all nodes of scale 2 in an MTG, *i.e.* all nodes except the Plant in our example:

```@example usepkg
mtg_2 = deepcopy(mtg)

delete_nodes!(mtg_2, scale = 2)
```

We can also remove nodes with more complex filters, for example all nodes with an index greater or equal to 1:

```@example usepkg
mtg_3 = deepcopy(mtg)

delete_nodes!(mtg_3, filter_fun = node -> node.MTG.index >= 1)
```

!!! note
    [`delete_nodes!`](@ref) always return the root node of the MTG. If the root node of the original MTG is deleted, the new node becomes the child of this node. If the root had several children, it returns an error. The function always return the root node of the new MTG, so if the root has not been modified, it remains the same, but if it has been deleted, the new root is returned. That is why it is preferable to use [`delete_nodes!`](@ref) has a non-mutating function and re-assign the results to an object if it is planned to remove the root node.

## Add nodes

We can add new nodes in an MTG programmatically using [`insert_nodes!`](@ref). The function insert new nodes based on a template NodeMTG provided by the user. The new nodes are inserted **before** the the filtered nodes, because it is impossible to decide beforehand if a new node would be considered as a new child or parent of the children otherwise. The name of the inserted node is automatically computed using [`new_name`](@ref).

For example if we need to insert new nodes before each leaf, we would do:

```@example usepkg
mtg_4 = deepcopy(mtg)
template = MutableNodeMTG("+", "Flower", 0, 2)
insert_nodes!(mtg_4, template, symbol = "Leaf")
```

The function does not do anything fancy, its sole job is to insert a new node at a given position with a template `NodeMTG`.
