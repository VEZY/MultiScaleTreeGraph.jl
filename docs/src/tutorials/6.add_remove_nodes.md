# Add/remove nodes

```@setup usepkg
using MultiScaleTreeGraph
```

## Make an MTG manually

It is very easy to add or remove nodes in an MTG. Actually, we can even construct an MTG completely manually.

Create the root node:

```@example usepkg
mtg = Node("node_1", MutableNodeMTG("/", "Plant", 0, 1), Dict{Symbol,Any}(:species => "Grassy-plant"))
```

The first argument to [`Node`](@ref) is the name. For the root node we usually use "node_1". The second argument is a [`MutableNodeMTG`](@ref) (or a [`NodeMTG`](@ref)) that defines the topology of the node: its link to its parent (the root node always decomposes), its symbol, its index and its scale. The third and last argument is used to add attributes to the MTG.

The names of the nodes must be unique and indexed. This package provides a function that guaranties a new name is unique in the MTG:

```@example usepkg
new_name = MultiScaleTreeGraph.new_name(mtg)
```

We can use this new name for naming the first child of the root node:

```@example usepkg
IN1 = Node(new_name, mtg, MutableNodeMTG("/", "Internode", 0, 2), Dict{Symbol,Any}(:diameter => 0.1, :length => 0.5))
```

To create a child node, we use a different method of [`Node`](@ref). This method is close to the one we used for the root, except there is a new argument at the second position: the parent node. Here we use the root node as the parent: `mtg`.

Now the MTG has two nodes:

```@example usepkg
mtg
```

We can continue like this indefinitely. For example we can add a leaf to the first internode:

```@example usepkg
Node(MultiScaleTreeGraph.new_name(mtg), IN1, MutableNodeMTG("+", "Leaf", 0, 2), Dict{Symbol,Any}(:area => 0.2))
```

!!! note
    If a node has no children, there is no need to keep track of it in an object.

And an internode following the first internode:

```@example usepkg
IN2 = Node(MultiScaleTreeGraph.new_name(mtg), IN1, MutableNodeMTG("<", "Internode", 1, 2), Dict{Symbol,Any}(:diameter => 0.15, :length => 0.3))
```

And a leaf to it:

```@example usepkg
Node(MultiScaleTreeGraph.new_name(mtg), IN2, MutableNodeMTG("+", "Leaf", 1, 2), Dict{Symbol,Any}(:area => 0.2))
```

And here is our resulting MTG:

```@example usepkg
mtg
```

And the attributes:

```@example usepkg
DataFrame(mtg, get_attributes(mtg))
```

## Insert/remove nodes programmatically

Sometimes we want to remove or add a lot of nodes in an MTG. This is possible to do it programmatically using [`delete_nodes!`](@ref) and [`insert_nodes`](@ref) respectively.
