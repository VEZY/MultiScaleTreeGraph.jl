# Computing

```@setup usepkg
using MultiScaleTreeGraph
file = joinpath(dirname(dirname(pathof(MultiScaleTreeGraph))),"test","files","simple_plant.mtg")
mtg = read_mtg(file)
```

An MTG can hold a lot of information, usually measured locally at one given scale. It is often interesting to compute new attributes based on the topological environment of the nodes.

For example one could be interested in computing how many leaves a plant has in total.

Let's first read our example MTG:

```@example usepkg
using MultiScaleTreeGraph

file = joinpath(dirname(dirname(pathof(MultiScaleTreeGraph))),"test","files","simple_plant.mtg")
mtg = read_mtg(file)
```

## Descendants

It is very easy to get the values of an attribute for all descendants of a node. For example to get the length attributes we would do:

```@example usepkg
descendants(mtg, :Length)
```

The `descendants` function visits every children nodes recursively until finding a leaf node. The function visits the branching nodes first.

## Ancestors

To get the values of an attribute from the ancestors of a node, we would similarly do:

```@example usepkg
node_5 = get_node(mtg, "node_5")
ancestors(node_5, :Length)
```

## Filters

Sometimes we only want the values of descendants or ancestors based on a given information. It is possible to filter out nodes based on their scale, symbol, link, or really anything by using the keyword arguments.

### Filter by scale

For example if we want the length of all descendants of the root node of our MTG that are of scale 3 (leaves & internodes), we would simply do:

```@example usepkg
descendants(mtg, :Length, scale = 3)
```

### Filter by symbol

If we need only the leaves, we would filter by their symbol (*i.e.* "Leaf"):

```@example usepkg
descendants(mtg, :Length, symbol = "Leaf")
```

### Filter by anything

And if we want to filter depending on an arbitrary value, we can use the `filter_fun` argument. For example if we want the length of the nodes, but only the ones with a width greater than 1, will would do like so:

```@example usepkg
descendants(mtg, :Length, filter_fun = x -> x[:Width] === nothing ? false : x[:Width] > 1)
```

!!! note
    The function passed to `filter_fun` must take a node as input, not attributes directly. This is because we want to be able to access any information the user could need. By using nodes as inputs we can even filter on a node parent or children, on a combination of its topological information, or by any of its attributes.

!!! warning
    By default if a node does not have an attribute, trying to get its value returns `nothing`. So if one uses attributes in the function passed to `filter_fun`, the function must handle missing values. This is what we do here by first testing if `x[:Width]` is `nothing` (in which case we return `false` to filter out the node), and then apply our test on the value of the node width.

### Filter helpers

There are three other arguments to help filtering nodes.

The first one is `all`. It is used to stop the search for new nodes as soon as one node does not correspond to the filters the user asked for.

It is generally used to get all nodes that are following a node for example. You can find an example usage [here](https://github.com/VEZY/Biomass_evaluation_LiDAR/blob/98576d0d2e9b9cfc9588451cc2bce42df68a6c87/1-code/functions.jl#L54) if you need one.

The second one is the `self` argument. It is used to return the value of the node on which we call the function if its `true`, and only the ancestors / descendants if `false` (the default).

The third one is the `recursivity_level`, that is used to control the depth of the search for the  ancestors / descendants. It is set to `-1` by default, which does not apply any filter on the depth. It is generally used to get *e.g.* only the children values of a node (`recursivity_level = 1`).

The fourth one is `ignore_nothing`. It is used to not return the values of a node if it is `nothing`. Note that it is applied **after** the filter, so `filter_fun` still has to handle `nothing` values.

## Transform values

### Assign attributes to a node

It is possible to change the values of attributes in a node. For example one could be interested to compute the total length of all nodes for the scene in our example MTG. In this case we can do:

```@example usepkg
mtg[:Length] = sum(descendants(mtg, :Length, ignore_nothing = true))
```

### Compute attributes in an MTG

Now MTGs can be very large, and it quickly becomes cumbersome to manually visit each node to change its value by hand.

Instead, you can compute new attributes for all nodes in an MTG using [`transform`](@ref). Head to the next tutorial for more information: [Transform an MTG](@ref).

## Helpers

Some helper functions can be useful when filtering nodes. For example you can use [`is_root`](@ref) to test if a node is the root node of the MTG. This is particularly useful when searching for ancestor values, but need a special treatment for the root node.

Similarly, you can use [`is_leaf`](@ref) to filter the leaf nodes of an MTG.

You also have [`nleaves`](@ref) to compute the number of leaf nodes on the sub-tree of a given node.
