# Transform an MTG

```@setup usepkg
using MultiScaleTreeGraph
file = joinpath(dirname(dirname(pathof(MultiScaleTreeGraph))),"test","files","simple_plant.mtg")
mtg = read_mtg(file)
```

## Transform

MTGs can be very large, and it quickly becomes impossible to manually change the attribute values of the nodes.

Instead, you can compute new attributes for all nodes in an MTG using [`transform!`](@ref).

The syntax of [`transform!`](@ref) is very close to the one from [`DataFrames.jl`](https://dataframes.juliadata.org/stable/).

### Rename an attribute

Renaming an attribute in an MTG is very simple, it is using the exact same syntax as for DataFrames. First, let's check which attributes are available in the MTG:

```@example usepkg
get_attributes(mtg)
```

Let's rename the `:Length` to remove the capital letter and make it all lowercase:

```@example usepkg
transform!(mtg, :Length => :length)
```

Let's check if the attribute name changed:

```@example usepkg
print(get_attributes(mtg))
```

Yes it did!

### Compute a new attribute based on other attributes

We can also compute a new attribute based on another. For example we could need the length in meters instead of centimetres. To do so, we can compute it as follow:

```@example usepkg
transform!(mtg, :Length => (x -> x / 10), ignore_nothing = true)
```

Here `transform!` take the `:Length` variable as input, and use it as the argument for the anonymous function `x -> x / 10`.

!!! warning
    The anonymous function has to be surrounded by parenthesis (like in `DataFrames.jl`).

Then it creates an automatic name for the new attribute based on the input variable name and the name of the function. If an anonymous function is used, the name "function" is used instead.

!!! note
    A lot of examples use anonymous functions. These functions are just a way to quickly define a function. For example a function that adds 1 to its input argument would usually be declared as:
    ```julia
    function test(x)
        x + 1
    end
    ```
    Here we have a name for our function: "test". But sometimes we don't need a name because the only job of our function is to be used in another function. In this case we can declare an anonymous function like so:
    ```julia
    x -> x + 1
    ```

In the case above, the new variable name is `:Length_function`, because we used an anonymous function. But if we used the `log` function instead, the output would be named `:Length_log` Here's an example with the `log` function:

```@example usepkg
transform!(mtg, :Length => log, ignore_nothing = true)
```

!!! note
    The `ignore_nothing` argument is set to `true` to filter-out the nodes with a value of `nothing` for the input variable, else it would error.

### Compute a new attribute based on node values

```@example usepkg
transform!(mtg, node -> node.MTG.Symbol => :Symbol)
```

With this form, you can also use the [`descendants`](@ref) and [`ancestors`](@ref) function too. For example to compute the total length of the subtree of each node in an MTG (*i.e.* the length of all children of a node), we would do:

```@example usepkg
function get_length_descendants(x)
    masses = descendants(x, :Length, ignore_nothing = true)
    if length(masses) == 0
        nothing
    else
        sum(masses)
    end
end

transform!(mtg, get_length_descendants => :length_subtree)
```

We first declared a new function that get the length of all descendants of a node, and then compute the sum only if some values of the length were found. Then we pass this function to `transform!` and define our new attribute name as `:length_subtree`. We define the function first for clarity because there is lot going on due to the possibility to get no values for length for some nodes.

An alternative way to write this would be:

```@example usepkg
transform!(
    mtg,
    (node -> descendants(node, :Length, ignore_nothing = true)) => :length_subtree,
    :length_subtree => x -> length(x) == 0 ? nothing : sum(x) => :length_subtree
)
```

Here we only use a call to `transform!` to do all the job. Because `transform!` computes everything sequentially, we can re-use a computation we just did before. This is what we use here. First we get the values of the length of all descendants of each node, and put the result in a new attribute `:length_subtree`. Then we re-use the data from this attribute to compute the sum, but only if the length of the data is not `0`, and put this result back to the same attribute `:length_subtree`.

### Apply a function to nodes

```@example usepkg
transform!(mtg, node -> isleaf(x) ? println(x.name," is a leaf") : nothing)
```

## Traverse

## Helpers

Some helper functions can be useful when filtering nodes.
nleaves
is_root
is_leaf ( can be used in `filter_fun`)
pipe_model
