# Manipulate nodes

```@setup usepkg
using MultiScaleTreeGraph
file = joinpath(dirname(dirname(pathof(MultiScaleTreeGraph))),"test","files","simple_plant.mtg")
mtg = read_mtg(file)
```

## Read an MTG file

Let's first read a simple MTG file:

```@example usepkg
using MultiScaleTreeGraph

file = joinpath(dirname(dirname(pathof(MultiScaleTreeGraph))),"test","files","simple_plant.mtg")
mtg = read_mtg(file)
```

!!! node
    If you print a node, it will always print its subtree. So if we print the root node it will print the entire MTG.

[`read_mtg`](@ref) returns a [`Node`](@ref) object.

```@example usepkg
typeof(mtg)
```

This node is the root node of the MTG, meaning the first node of the MTG, the one without any parent.

!!! note
    Root node and leaf node mean a node without any parent or children respectively. These terms are used in the sense of a [tree data structure](https://en.wikipedia.org/wiki/Tree_(data_structure)).

## The Node type

### What's a node?

As we learned in the previous section, the node is used as the elementary object to build the MTG. In this package, a node is a data structure used to hold these informations (*i.e.* fields):

- name: the name of the node. It is usually generated when read from a file and unique in the MTG;
- parent: the parent node;
- children: a dictionary of the children nodes, or `nothing` if no children;
- siblings: a dictionary of sibling(s) node(s) if any, or else `nothing`. Can be `nothing` if not computed too;
- MTG: the MTG encoding of the node (see below, or [`NodeMTG`](@ref));
- attributes: the node attributes. Can be a `NamedTuple`, a `MutableNamedTuple` or a `Dict` or similar (e.g. `OrderedDict`). The choice of the data structure used depends on how much you plan to change the attributes and their values. Attributes include for example the length or diameter of a node, its colour, 3d position...

### Get the node's fields

To get a field of a node, you can use the dot notation, *e.g.*:

```@example usepkg
mtg.attributes
```

The `MTG` field from the node helps us describe the node within the MTG. Let's see what's in it:

```@example usepkg
fieldnames(typeof(mtg.MTG))
```

We see that it holds the MTG fields: the scale, symbol, index and link to its parent.

### Get other nodes

We can move in the MTG from node to node because we know its parents and children.

To do so, we use either the dot notation, or the corresponding functions.

#### Get children nodes

To get the children of a node, you can use either:

```@example usepkg
mtg.children
```

or:

```@example usepkg
children(mtg)
```

You can also index the node using an integer, and it will return the corresponding child:

```@example usepkg
mtg[1]
```

#### Get the parent node

To get the parent you can use:

```@example usepkg
mtg.parent
```

Or:

```@example usepkg
parent(mtg)
```

Note that it returns `nothing` here because the root node has no parent.


#### Get the node siblings

To get the node siblings:

```@example usepkg
siblings(mtg[1][1][1][1])
```

!!! note
    We repeatedly index into the MTG (`mtg[1][1][1][1]`) to get the fourth generation descendant of the root node, because it is the only one with a sibling in our example MTG.

#### Get any node

To get any node in the MTG, you can get it by its name:

```@example usepkg
get_node(mtg, "node_5")
```

#### Get the root node

To get the root node from any other node, simply use [`get_root`](@ref):

```@example usepkg
node_5 = get_node(mtg, "node_5")

get_root(node_5)
```


### Get the attributes

You can get all node attributes using either the dot notation:


```@example usepkg
node_5.attributes
```

Then you can get the attributes using Symbol or String indexing if the attributes are stored as a Dictionary, or again the dot notation if it's a NamedTuple-alike (see [Attributes type](@ref) for more details). In our case it is a Dictionary (Dict), so we can do:

```@example usepkg
node_5.attributes[:Length]
```

Or you can simply use the more standard way of indexing the node with a String or a Symbol directly:

```@example usepkg
node_5[:Length]
```

!!! note
    This is the preferred way to retrieve node attributes as the call does not depend on the type of your attributes, making your code more generic.

To get all attributes available in the node subtree, you can use [`get_features`](@ref):

```@example usepkg
get_features(node_5)
```

Note that it returns only two attributes here because "node_5" is a leaf, so it has no sub-tree, and hence [`get_features`](@ref) only returns the node's attributes. To be sure to get all the attributes available in the MTG, it is better to call [`get_features`](@ref) on the root node:

```@example usepkg
get_features(mtg)
```

If you start from another node you can retrieve the root node using [`get_root`](@ref):

```@example usepkg
get_features(get_root(node_5))
```
